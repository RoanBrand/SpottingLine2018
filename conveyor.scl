FUNCTION_BLOCK "conveyor"
{ S7_Optimized_Access := 'FALSE' }
VERSION : 0.1
   VAR_INPUT 
      usrBtn { ExternalVisible := 'False'} : Bool;   // User Button. Press (True) to initiate product send.
      prdSens : Bool;   // Product Sensor. True-> Product present.
      eStop { ExternalVisible := 'False'} : Bool;   // Emergency stop button.
      vsdReady { ExternalVisible := 'False'} : Bool;   // VSD Online and Ready. False-> Conveyor VSD off or in Error State.
      xManualMode : Bool;   // Manual mode request.
      xOffload { ExternalVisible := 'False'} : Bool;   // Set if this is offloading conveyor.
      xOnload { ExternalVisible := 'False'} : Bool;   // Set if onloading conveyor. User button can be pressed to start conveyor to receive a block. (Usefull for operator loading)
      comms_from_prev_conv { ExternalVisible := 'False'} : Int;   // Communication input from previous conveyor in line.
      comms_from_next_conv { ExternalVisible := 'False'} : Int;   // Communication input from next conveyor in line.
      prd_in { ExternalVisible := 'False'} : "Product";
      xReverseSend : Bool;   // Option: True-> Prd sent to previous conv in reverse. False (default)-> Prd sent to next conv.
   END_VAR

   VAR_OUTPUT 
      rollersFwd { ExternalVisible := 'False'} : Bool;   // True-> Conveyor belt on. Product moving forward.
      rollersRev { ExternalVisible := 'False'} : Bool;   // True-> Conveyor belt on. Product moving in reverse.
      statusLed { ExternalVisible := 'False'} : Bool;   // Light Indication to user.
      comms_to_prev_conv { ExternalVisible := 'False'} : Int;   // Communication output to previous conveyor in line.
      comms_to_next_conv { ExternalVisible := 'False'} : Int;   // Communication output to next conveyor in line.
      prd_out { ExternalVisible := 'False'} : "Product";
      alarms_out : Word;
      alarms { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} AT alarms_out : Array[0..15] of Bool;
   END_VAR

   VAR 
      state : Int;   // Conveyor state. 0->Startup; 1->ReadyToReceive; 2->Receiving; 3->ReadyToSend; 4->Sending; 5-> Error;
      state_prev { ExternalVisible := 'False'} : Int;   // Previous conveyor state.
      prd : "Product";
      t_off {OriginalPartName := 'TOF'; LibVersion := '1.0'; ExternalVisible := 'False'} : TOF;
      t_on {OriginalPartName := 'TON'; LibVersion := '1.0'; ExternalVisible := 'False'} : TON;
      m_rollersFwd : Bool;   // Manual rollers motor forward request.
      m_rollersRev : Bool;   // Manual rollers motor reverse request.
   END_VAR

   VAR CONSTANT 
      s_startup : Int := 0;
      s_ready_to_receive : Int := 1;
      s_receiving : Int := 2;
      s_ready_to_send : Int := 3;
      s_sending : Int := 4;
      s_reset : Int := 5;
      s_manual : Int := 10;
      m_silent : Int := 0;   // Inter-conveyor message (To both) : No message
      m_req_send : Int := 100;   // Inter-conveyor message (To next) : Request to send product
      m_perm_send : Int := 101;   // Inter-conveyor message (To prev) : Permission to send product
      m_sending : Int := 102;   // Inter-conveyor message (To next) : Currently  sending product
      c_conveyor_timeout : Time := T#25S;   // Time to wait for product to arrive or leave the conveyor
   END_VAR


BEGIN
	// Simple, single direction conveyor.
	// Push user button to send product to next conveyor in line.
	
	IF #state <> #s_reset THEN
	    // Handle EStop and VSD error and reset conveyor.
	    IF (#eStop OR NOT #vsdReady) THEN
	        #state := #s_reset;
	    END_IF;
	END_IF;
	
	// Conveyor state machine.
	CASE #state OF
	    0:  // Startup
	        IF #state_prev <> #state THEN
	            #state_prev := #state;
	        END_IF;
	        
	        IF #prdSens THEN
	            #state := #s_ready_to_send;
	        ELSE
	            #state := #s_ready_to_receive;
	        END_IF;
	        ;
	    1:  // Ready to Receive
	        IF #state_prev <> #state THEN
	            #t_off(IN := false,
	                   PT := t#1s);
	            #state_prev := #state;
	        END_IF;
	        
	        IF #prdSens THEN
	            #comms_to_prev_conv := #m_silent;
	            #state := #s_ready_to_send;
	        ELSIF #xManualMode THEN
	            #comms_to_prev_conv := #m_silent;
	            #state := #s_manual;
	        ELSIF #xOnload AND #usrBtn THEN
	            #state := #s_receiving;
	        ELSE
	            #t_off(IN := #comms_from_prev_conv = #m_req_send,
	                   PT := t#1s);
	            
	            IF #t_off.Q THEN
	                #comms_to_prev_conv := #m_perm_send;
	                IF #comms_from_prev_conv = #m_sending THEN
	                    #state := #s_receiving;
	                END_IF;
	            ELSE
	                #comms_to_prev_conv := #m_silent;
	            END_IF;
	        END_IF;
	        ;
	    2:  // Receiving
	        IF #state_prev <> #state THEN
	            #t_on(IN := false,
	                  PT := #c_conveyor_timeout);
	            #prd := #prd_in;
	            #rollersFwd := true;
	            #state_prev := #state;
	        END_IF;
	        
	        #t_on(IN := true,
	              PT := #c_conveyor_timeout);
	        
	        IF #prdSens THEN
	            #state := #s_ready_to_send;
	        ELSE
	            IF #t_on.Q THEN
	                // Timeout error - Product did not arrive in time.
	                #alarms[0] := true;
	                #state := #s_reset;
	            END_IF;
	        END_IF;
	        
	        IF #state_prev <> #state THEN
	            #comms_to_prev_conv := #m_silent;
	            #rollersFwd := false;
	        END_IF;
	        ;
	    3:  // Ready to Send
	        IF #state_prev <> #state THEN
	            #state_prev := #state;
	        END_IF;
	        
	        IF NOT #prdSens THEN
	            // Product mysteriously dissapeared.
	            #comms_to_prev_conv := #m_silent;
	            #comms_to_next_conv := #m_silent;
	            #alarms[2] := NOT #xOffload;
	            #state := #s_reset;
	        ELSIF #xManualMode THEN
	            #comms_to_prev_conv := #m_silent;
	            #comms_to_next_conv := #m_silent;
	            #state := #s_manual;
	        ELSE
	            #t_off(IN := #usrBtn,
	                   PT := t#1s);
	            
	            IF #t_off.Q THEN
	                IF #xReverseSend THEN
	                    #comms_to_prev_conv := #m_req_send;
	                    IF #comms_from_prev_conv = #m_perm_send THEN
	                        #state := #s_sending;
	                    END_IF;
	                ELSE
	                    #comms_to_next_conv := #m_req_send;
	                    IF #comms_from_next_conv = #m_perm_send THEN
	                        #state := #s_sending;
	                    END_IF;
	                END_IF;
	            ELSE
	                #comms_to_prev_conv := #m_silent;
	                #comms_to_next_conv := #m_silent;
	            END_IF;
	        END_IF;
	        ;
	    4:  // Sending
	        IF #state_prev <> #state THEN
	            #t_on(IN := false,
	                  PT := #c_conveyor_timeout);
	            #prd_out := #prd;
	            IF #comms_from_next_conv = #m_perm_send THEN
	                #rollersFwd := true;
	                #comms_to_next_conv := #m_sending;
	            ELSE
	                #rollersRev := true;
	                #comms_to_prev_conv := #m_sending;
	            END_IF;
	            #state_prev := #state;
	        END_IF;
	        
	        #t_on(IN := true,
	              PT := #c_conveyor_timeout);
	        
	        IF NOT #prdSens THEN
	            IF (#rollersFwd AND (#comms_from_next_conv <> #m_perm_send))
	                OR (#rollersRev AND (#comms_from_prev_conv <> #m_perm_send)) THEN
	                #state := #s_ready_to_receive;
	            END_IF;
	        ELSE
	            IF #t_on.Q THEN
	                // Timeout error - Product did not leave in time.
	                #alarms[1] := true;
	                #state := #s_reset;
	            END_IF;
	        END_IF;
	        
	        IF #state_prev <> #state THEN
	            #rollersFwd := false;
	            #rollersRev := false;
	            #comms_to_next_conv := #m_silent;
	            #comms_to_prev_conv := #m_silent;
	        END_IF;
	        ;
	    5:  // Reset
	        IF #state_prev <> #state THEN
	            #rollersFwd := false;
	            #rollersRev := false;
	            #m_rollersFwd := false;
	            #m_rollersRev := false;
	            #statusLed := false;
	            #comms_to_next_conv := 0;
	            #comms_to_prev_conv := 0;
	            #t_on(IN := false,
	                  PT := t#2s);
	            #state_prev := #state;
	        END_IF;
	        
	        #t_on(IN := true,
	              PT := t#2s);
	        
	        IF #t_on.Q THEN
	            #alarms_out := 0;
	        END_IF;
	        
	        #alarms[8] := NOT #vsdReady AND NOT #eStop;
	        
	        IF #t_on.Q
	            AND NOT #eStop AND #vsdReady
	        THEN
	            #alarms_out := 0;
	            #state := #s_startup;
	        END_IF;
	        ;
	    10:  // Manual Mode
	        IF #state_prev <> #state THEN
	            #state_prev := #state;
	        END_IF;
	        
	        #rollersFwd := #m_rollersFwd;
	        #rollersRev := #m_rollersRev;
	        
	        IF NOT #xManualMode THEN
	            #state := #s_reset;
	        END_IF;
	        
	        IF #state_prev <> #state THEN
	            #rollersFwd := false;
	            #rollersRev := false;
	            #m_rollersFwd := false;
	            #m_rollersRev := false;
	        END_IF;
	        ;
	END_CASE;
	
END_FUNCTION_BLOCK

